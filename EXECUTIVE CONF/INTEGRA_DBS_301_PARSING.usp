#SYMBOL_NAME "INTEGRA_DBS_301_PARSING"
#HINT "SEE INTEGRA DBS-30.1 MODULE"
#CATEGORY "10" // SERIAL
#DEFAULT_VOLATILE

#DEFINE_CONSTANT STRLEN 200  
#DEFINE_CONSTANT TRUE 1
#DEFINE_CONSTANT FALSE 0

DIGITAL_INPUT DEBUG;
BUFFER_INPUT RX$[STRLEN];   

DIGITAL_OUTPUT POWER_FB;
ANALOG_OUTPUT TRANSPORT, MEDIA, DIMMER, ASPECT;                      
ANALOG_OUTPUT HDMI_RESOLUTION, HDMI_SETUP, LANGUAGE;
ANALOG_OUTPUT TITLE_CURRENT, TITLE_TOTAL;
ANALOG_OUTPUT CHAPTER_CURRENT, CHAPTER_TOTAL;  
ANALOG_OUTPUT TIME_HOURS, TIME_MINUTES, TIME_SECONDS, TIME_TOTALSEC;



// GLOBAL VARIABLES
STRING KEY$[12][4];       // RESPONSE PREFIX GRID

// FUNCTIONS
FUNCTION PARSE(STRING INPUT$, INTEGER TYPE)
{            
INTEGER X;
IF(RIGHT(INPUT$,3)="FF\x1A")  RETURN; // UNKNOWN RESPONSE
SWITCH (TYPE)
	{
	CASE(01): // STATUS MODE
		{
		//
		}	
	CASE(02): // ACTION STATUS
		{   
		X=ATOI(RIGHT(INPUT$,3));
		IF(X=0) POWER_FB=FALSE;   
		IF(X>0 && X<4) 
			{
			TRANSPORT=X;
			POWER_FB=TRUE;
			}
		} 
	CASE(03): // DISC STATUS
		{
		MEDIA=ATOI(RIGHT(INPUT$,3));
		}   
	CASE(04): // DIMMER LEVEL
		{
		DIMMER=ATOI(RIGHT(INPUT$,3));
		} 
	CASE(05): // ASPECT
		{
		ASPECT=ATOI(RIGHT(INPUT$,3));
		}     
	CASE(06): // TITLE/GROUP/FOLDER
		{
		TITLE_CURRENT=ATOI(MID(INPUT$,6,3));
		TITLE_TOTAL=ATOI(MID(INPUT$,9,3));		
		}    
	CASE(07): // CHAPTER/TRACK
		{
		CHAPTER_CURRENT=ATOI(MID(INPUT$,6,3));
		CHAPTER_TOTAL=ATOI(MID(INPUT$,9,3));		
        }
    CASE(08): // ELAPSED TIME
    	{
    	TIME_HOURS=ATOI(MID(INPUT$,6,1));
    	TIME_MINUTES=ATOI(MID(INPUT$,7,2));
    	TIME_SECONDS=ATOI(MID(INPUT$,9,2));
    	TIME_TOTALSEC=(TIME_HOURS*60*60)+(TIME_MINUTES*60)+TIME_SECONDS;
    	}    
    CASE(09): // HDMI RESOLUTION OUT
    	{
    	HDMI_RESOLUTION=ATOI(RIGHT(INPUT$,3));
		}   
	CASE(10): // LANGUAGE
		{
		IF(MID(INPUT$,6,2)="EN") LANGUAGE=1;
		IF(MID(INPUT$,6,2)="FR") LANGUAGE=2;
		IF(MID(INPUT$,6,2)="SP") LANGUAGE=3;   
		}   
	CASE(11): // CEC, WE TURNED THIS OFF IN SETUP
		{
		//
		}
	CASE(12): // ANOTHER HDMI OUTPUT FLAG
		{
		HDMI_SETUP=ATOI(RIGHT(INPUT$,3)); 
		IF(RIGHT(INPUT$,3)="OF\x1A") HDMI_SETUP=7; // HDMI OFF STATUS
		}
	}
}

CHANGE RX$
{
INTEGER CTR;
STRING TEMP$[STRLEN];
TEMP$=GATHER("\x1A",RX$);  // EOF FOR INTEGRA RESPONSE
FOR(CTR=1 TO 12)
	{
	IF (FIND(KEY$[CTR],TEMP$)) PARSE(TEMP$,CTR);
	}     
IF(DEBUG) PRINT(">>%s<<", TEMP$);
}

FUNCTION MAIN()
{
// IDENTIFY RESPONSE PREFIX GRID
KEY$[00]="";     // DEFAULT NULL
KEY$[01]="SPM";  // STATUS MODE NOTICE
KEY$[02]="SST";  // STATUS ACTION NOTICE
KEY$[03]="DST";  // CURRENT DISC STATUS NOTICE
KEY$[04]="MST";  // DIMMER LEVEL STATUS NOTICE
KEY$[05]="AST";  // ASPECT RATIO STATUS
KEY$[06]="STG";  // TITLE/GROUP/FOLDER NO STATUS NOTICE
KEY$[07]="STC";  // CHAPTER/TRACK NO STATUS NOTICE
KEY$[08]="SET";  // ELAPSED TIME STATUS NOTICE
KEY$[09]="SMO";  // HDMI OUT PICTURE RESOLUTION STATUS NOTICE
KEY$[10]="SCM";  // LANGUAGE SET STATUS NOTICE
KEY$[11]="SCC";  // CEC CONTROL SETUP STATUS NOTICE
KEY$[12]="SMR";  // HDMI RESOLUTION SETUP STATUS NOTICE
}
